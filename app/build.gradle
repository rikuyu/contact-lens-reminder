plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.google.gms.google-services"
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "io.github.rikuyu.contactlensreminder"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'

        // @OptIn(ExperimentalXXXXApi::class) warningを消す
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
        kotlinCompilerVersion '1.5.21'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lintOptions {
        xmlReport true
    }
}

configurations {
    ktlint
}

dependencies {

    implementation appDependencies.core
    implementation appDependencies.ui
    implementation appDependencies.appcompat
    implementation appDependencies.material
    implementation appDependencies.composeMaterial
    implementation appDependencies.preview
    implementation appDependencies.lifecycleRuntime
    implementation appDependencies.activity

    testImplementation testDependencies.junitKtx
    testImplementation testDependencies.coreKtx
    testImplementation testDependencies.coreTesting
    testImplementation testDependencies.truth

    implementation navigationDependencies.navigation

    implementation uiDependencies.numberPicker
    implementation uiDependencies.lifecyclViewModelCompose
    implementation uiDependencies.pager
    implementation uiDependencies.pagerIndicator

    implementation diDependencies.hiltAndroid
    kapt diDependencies.hiltAndroidCompiler
    implementation diDependencies.hiltLifecycleViewModel
    kapt diDependencies.hiltCompiler
    implementation diDependencies.hiltNavigationCompose

    implementation platform(firebaseDependencies.firebaseBom)
    implementation firebaseDependencies.firebaseAnalyticsKtx
    implementation firebaseDependencies.firebaseCrashlyticsKtx

    ktlint("com.pinterest:ktlint:0.42.1")
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--android", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/ktlint-result.xml", "src/**/*.kt"
    ignoreExitValue true
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
    ignoreExitValue true
}
